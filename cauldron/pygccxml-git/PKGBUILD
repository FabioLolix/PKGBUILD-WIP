# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Yunhui Fu <yhfudev at gmail dot com>
# Contributor: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: zarra <zarraxx@gmail.com>

pkgname=python-pygccxml-git
pkgver=3.0.0.r0.g7e9c8cf
pkgrel=1
pkgdesc="Simple framework to navigate C++ declarations, using Python classes."
arch=(any)
url="https://github.com/gccxml/pygccxml"
license=(Boost)
depends=(castxml python)
makedepends=(git python-build python-wheel python-installer python-setuptools python-jaraco.collections)
checkdepends=(python-pytest) # python-pycodestyle
provides=(python-pygccxml pygccxml)
conflicts=(python-pygccxml pygccxml)
replaces=(pygccxml)
source=("git+https://github.com/gccxml/pygccxml.git#tag=v3.0.0" # tmp use of a tag
        0001-pygccxml-tests.patch)
sha256sums=('8d5c0357da933ae67ed924491b05bb1422fa2e21eb1cc15049bec354aa782966'
            '2ca5fb71a8cc5540a47c57a94befcba21c7b0ab2fb3938c1f5c56e1299b1fde8')

prepare() {
  cd pygccxml
  patch -Np1 -i ../0001-pygccxml-tests.patch
}

pkgver() {
  cd pygccxml
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd pygccxml
  python -m build --wheel --no-isolation
}

check() {
  cd pygccxml
  pytest -v
}

package() {
  cd pygccxml
  python -m installer --destdir="$pkgdir" dist/*.whl
}
