# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Yunhui Fu <yhfudev at gmail dot com>
# Contributor: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: zarra <zarraxx@gmail.com>

pkgname=python-pygccxml-git
pkgver=2.4.0.r134.gd0eeab1
pkgrel=1
pkgdesc="Simple framework to navigate C++ declarations, using Python classes."
arch=(any)
url="https://github.com/gccxml/pygccxml"
license=(Boost)
depends=(castxml python)
makedepends=(git python-build python-wheel python-installer python-setuptools python-jaraco.collections)
#checkdepends=(python-pycodestyle)
checkdepends=(python-pytest)
provides=(python-pygccxml pygccxml)
conflicts=(python-pygccxml pygccxml)
replaces=(pygccxml)
source=("git+https://github.com/gccxml/pygccxml.git")
sha256sums=('SKIP')

pkgver() {
  cd pygccxml
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd pygccxml
  python -m build --wheel --no-isolation
}

check() {
  cd pygccxml
  pytest -v
}

package() {
  cd pygccxml
  python -m installer --destdir="$pkgdir" dist/*.whl
}
