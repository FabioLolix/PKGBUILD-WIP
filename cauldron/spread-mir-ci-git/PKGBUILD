# Maintainer: Your Name <youremail@domain.com>

pkgname=spread-mir-ci-git
pkgver=r312.50434fe
pkgrel=1
pkgdesc="Convenient full-system test (task) distribution, Mir CI fork"
arch=(x86_64)
url="https://github.com/canonical/spread"
license=(GPL-3.0-only)
depends=()
makedepends=(git go)
provides=(spread)
conflicts=(spread)
source=("git+https://github.com/MirServer/spread-mir-ci.git")
sha256sums=('SKIP')

pkgver() {
  cd spread-mir-ci
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd spread-mir-ci/cmd/spread
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .

  cd "${srcdir}/spread-mir-ci/cmd/humbox"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .

}

package() {
  cd spread-mir-ci/cmd
  install -D spread/spread -t ${pkgdir}/usr/bin
  install -D humbox/humbox -t ${pkgdir}/usr/bin
}
