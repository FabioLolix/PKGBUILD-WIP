# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=zathuradbg-git
pkgver=0.3.4.beta.r1.g72c7d64
pkgrel=1
pkgdesc="Emulation based tool for learning and debugging assembly"
arch=(x86_64)
url="https://github.com/ZathuraDbg/ZathuraDbg"
license=(GPL-3.0-only)
depends=(glibc gcc-libs glfw libglvnd

    )
makedepends=(
    git
    cmake
    boost
    chrpath

    unicorn
    capstone
    keystone
    )
provides=(zathuradbg)
conflicts=(zathuradbg)
options=(!lto)
source=("git+https://github.com/ZathuraDbg/ZathuraDbg.git"
        "git+https://github.com/ocornut/imgui.git"
        "git+https://github.com/Tessil/ordered-map.git"
        "git+https://github.com/HACKE-RC/ImGuiColorTextEdit.git"
        "git+https://github.com/unicorn-engine/unicorn.git"
        "tinyfiledialogs::git+http://git.code.sf.net/p/tinyfiledialogs/code.git"
        "git+https://github.com/gpakosz/whereami.git"
        "git+https://github.com/boostorg/regex.git"
        "git+https://github.com/capstone-engine/capstone.git"
        "git+https://github.com/keystone-engine/keystone.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ZathuraDbg
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

# boost is needed by ImGuiColorTextEdit

# https://github.com/ZathuraDbg/ZathuraDbg/issues/95
# Really use the system version
# zathuradbg-git E: Dependency unicorn detected and not included (libraries ['usr/lib/libunicorn.so.2'] needed in files ['usr/bin/zathuradbg'])
# zathuradbg-git E: Dependency keystone detected and not included (libraries ['usr/lib/libkeystone.so.0'] needed in files ['usr/bin/zathuradbg'])
# zathuradbg-git E: Dependency capstone detected and not included (libraries ['usr/lib/libcapstone.so.5'] needed in files ['usr/bin/zathuradbg'])

# Using git submodules can't link them
# [100%] Linking CXX executable /build/zathuradbg-git/src/ZathuraDbg/src/Zathura
# /usr/bin/ld: cannot find -lunicorn: No such file or directory
# /usr/bin/ld: cannot find -lkeystone: No such file or directory
# /usr/bin/ld: cannot find -lcapstone: No such file or directory
# collect2: error: ld returned 1 exit status

# keystone: want the submodule (but can't link it)

# zathuradbg-git W: Insecure RUNPATH '/usr/local/lib' in file ('usr/bin/zathuradbg')

prepare() {
  cd "${srcdir}/ImGuiColorTextEdit"
  git submodule init
  git config submodule.vendor/regex.url "${srcdir}/regex"
  git -c protocol.file.allow=always submodule update

  cd "${srcdir}/ZathuraDbg"
  git submodule init
  git config submodule.vendor/imgui.url "${srcdir}/imgui"
  git config submodule.imgui.url "${srcdir}/imgui"
  git config submodule.vendor/ordered-map.url "${srcdir}/ordered-map"
  git config submodule.vendor/ImGuiColorTextEdit.url "${srcdir}/ImGuiColorTextEdit"
  git config submodule.vendor/unicorn.update none
  #git config submodule.vendor/unicorn.url "${srcdir}/unicorn"
  git config submodule.vendor/code.url "${srcdir}/tinyfiledialogs"
  git config submodule.vendor/tinyfdialogue.update none
  #git config submodule.vendor/whereami.url "${srcdir}/whereami"
  git config submodule.vendor/capstone.url "${srcdir}/capstone"
  #git config submodule.vendor/capstone.update none
  git config submodule.vendor/keystone.url "${srcdir}/keystone"
  #git config submodule.vendor/keystone.update none
  git -c protocol.file.allow=always submodule update
}

build() {
  local _flags=(

  )

  cmake -B build -S "ZathuraDbg/src" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

package() {
  #DESTDIR="${pkgdir}" cmake --install build
  install -D ZathuraDbg/src/Zathura "${pkgdir}"/usr/bin/zathuradbg
  chrpath --delete "${pkgdir}/usr/bin/zathuradbg"
}
