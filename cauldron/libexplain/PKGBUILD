# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libexplain
pkgver=1.4
_debver=D001-16
pkgrel=1
pkgdesc="Library which may be used to explain Unix and Linux error messages"
arch=(x86_64)
url="https://sourceforge.net/projects/libexplain/"
license=(GPL-3.0-or-later)
depends=()
makedepends=()
options=()
source=("https://sourceforge.net/projects/libexplain/files/${pkgver}/libexplain-${pkgver}.tar.gz"
        "http://deb.debian.org/debian/pool/main/libe/libexplain/libexplain_${pkgver}.${_debver}.debian.tar.xz")
sha256sums=('28863b65eccc74934e237cac41364cb3c1802c36c9e2318ed0417460fee83f80'
            '1a63d62c53c2ccd5b311778cd4b705bc94818d424b29b172793c8f196a4e2951')

prepare() {
  cd "libexplain-${pkgver}"
  for patch in ../debian/patches/*.patch; do 
    patch -Np1 < $patch
  done
}

build() {
  cd "libexplain-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "libexplain-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
